/*-*- mode:asm; indent-tabs-mode:t; tab-width:8; coding:utf-8               -*-│
│vi: set et ft=asm ts=8 tw=8 fenc=utf-8                                     :vi│
╞══════════════════════════════════════════════════════════════════════════════╡
│ Copyright 2020 Justine Alexandra Roberts Tunney                              │
│                                                                              │
│ This program is free software; you can redistribute it and/or modify         │
│ it under the terms of the GNU General Public License as published by         │
│ the Free Software Foundation; version 2 of the License.                      │
│                                                                              │
│ This program is distributed in the hope that it will be useful, but          │
│ WITHOUT ANY WARRANTY; without even the implied warranty of                   │
│ MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU             │
│ General Public License for more details.                                     │
│                                                                              │
│ You should have received a copy of the GNU General Public License            │
│ along with this program; if not, write to the Free Software                  │
│ Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA                │
│ 02110-1301 USA                                                               │
╚─────────────────────────────────────────────────────────────────────────────*/
#include "libc/macros.h"
#include "libc/notice.inc"
#include "libc/sysv/consts/auxv.h"
.source	__FILE__

/	Canary for -fstack-protector.
/
/	This global is referenced by synthetic code generated by GCC.
/	The -mstack-protector-guard=global flag might need to be passed.
/
/	@note	this value is protected by piro
	.initbss 301,_init___stack_chk_guard
__stack_chk_guard:
	.quad	0
	.endobj	__stack_chk_guard,globl
	.previous

	.init.start 301,_init___stack_chk_guard
	mov	kStartTsc(%rip),%rax
	mov	AT_RANDOM,%edx		# modern linux provides this
	test	%edx,%edx
	jz	1f
	pushpop	-16,%rcx
0:	add	$16,%rcx		# ax ^= di[1][0] if di[0]=AT_RANDOM
	cmpq	$0,(%r15,%rcx)		# %r15 is qword(*auxv)[2]
	jz	1f			# @see libc/crt/crt.S
	cmp	%rdx,(%r15,%rcx)
	jne	0b
	mov	8(%r15,%rcx),%rcx
	xor	(%rcx),%rax
1:	stosq
	.init.end 301,_init___stack_chk_guard
